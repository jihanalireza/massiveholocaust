{% load static %}
<!DOCTYPE html>
<html lang="en" >

<head>
  <meta charset="UTF-8">
  <title>particles curl noise</title>
  <link rel="shortcut icon" href="http://maternaldisaster.com/public/assets/img/ico.png" type="image/x-icon" />
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/normalize/5.0.0/normalize.min.css">


  <link rel="stylesheet" href="{% static "vendor/particular/css/style.css" %}">


</head>

<body>

  <script type = 'x-shader/x-vertex' id='chromatic_aberration_vs'>
  varying vec2 vUv;

  void main() {

    vUv = uv;
    gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

  }

  </script>

  <script type = 'x-shader/x-fragment' id = 'chromatic_aberration_fs'>
  precision highp float;
  uniform vec2 resolution;
  uniform sampler2D tDiffuse;
  uniform float power;
  varying vec2 vUv;

  float smootherstep(float edge0, float edge1, float x) {
    x = clamp((x - edge0)/(edge1 - edge0), 0.0, 1.0);
    return x*x*x*(x*(x*6. - 15.) + 10.);
  }

  vec4 rgbShift(sampler2D tDiffuse, vec2 uv, vec2 delta) {
    vec2 dir = uv - vec2( .5 );
    float d = power * length( dir );
    normalize( dir );
    vec2 value = d * dir * delta;
    vec4 c1 = texture2D( tDiffuse, uv - value / resolution.x );
    vec4 c2 = texture2D( tDiffuse, uv );
    vec4 c3 = texture2D( tDiffuse, uv + value / resolution.y );
    return vec4( c1.r, c2.g, c3.b, c1.a + c2.a + c3.b );
  }

  void main() {

    gl_FragColor = rgbShift(tDiffuse, vUv,resolution/40.);

  }

  </script>

  <script type = 'x-shader/x-vertex' id='vertexShader'>

  uniform float time;

//	Simplex 4D Noise 
//	by Ian McEwan, Ashima Arts
//
vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
float permute(float x){return floor(mod(((x*34.0)+1.0)*x, 289.0));}
vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}
float taylorInvSqrt(float r){return 1.79284291400159 - 0.85373472095314 * r;}

vec4 grad4(float j, vec4 ip){
  const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
  vec4 p,s;

  p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
  p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
  s = vec4(lessThan(p, vec4(0.0)));
  p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www; 

  return p;
}

float snoise(vec4 v){
const vec2  C = vec2( 0.138196601125010504,  // (5 - sqrt(5))/20  G4
0.309016994374947451); // (sqrt(5) - 1)/4   F4
// First corner
vec4 i  = floor(v + dot(v, C.yyyy) );
vec4 x0 = v -   i + dot(i, C.xxxx);

// Other corners

// Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
vec4 i0;

vec3 isX = step( x0.yzw, x0.xxx );
vec3 isYZ = step( x0.zww, x0.yyz );
//  i0.x = dot( isX, vec3( 1.0 ) );
i0.x = isX.x + isX.y + isX.z;
i0.yzw = 1.0 - isX;

//  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
i0.y += isYZ.x + isYZ.y;
i0.zw += 1.0 - isYZ.xy;

i0.z += isYZ.z;
i0.w += 1.0 - isYZ.z;

// i0 now contains the unique values 0,1,2,3 in each channel
vec4 i3 = clamp( i0, 0.0, 1.0 );
vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

//  x0 = x0 - 0.0 + 0.0 * C 
vec4 x1 = x0 - i1 + 1.0 * C.xxxx;
vec4 x2 = x0 - i2 + 2.0 * C.xxxx;
vec4 x3 = x0 - i3 + 3.0 * C.xxxx;
vec4 x4 = x0 - 1.0 + 4.0 * C.xxxx;

// Permutations
i = mod(i, 289.0); 
float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
vec4 j1 = permute( permute( permute( permute (
  i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
+ i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
+ i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
+ i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));
// Gradients
// ( 7*7*6 points uniformly over a cube, mapped onto a 4-octahedron.)
// 7*7*6 = 294, which is close to the ring size 17*17 = 289.

vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

vec4 p0 = grad4(j0,   ip);
vec4 p1 = grad4(j1.x, ip);
vec4 p2 = grad4(j1.y, ip);
vec4 p3 = grad4(j1.z, ip);
vec4 p4 = grad4(j1.w, ip);

// Normalise gradients
vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
p0 *= norm.x;
p1 *= norm.y;
p2 *= norm.z;
p3 *= norm.w;
p4 *= taylorInvSqrt(dot(p4,p4));

// Mix contributions from the five corners
vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
m0 = m0 * m0;
m1 = m1 * m1;
return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
  + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

}

vec3 snoiseVec3( vec3 x ){

  float s  = snoise(vec4(vec3( x ),time));
  float s1 = snoise(vec4(vec3( x.y - 19.1 , x.z + 33.4 , x.x + 47.2 ),time));
  float s2 = snoise(vec4(vec3( x.z + 74.2 , x.x - 124.5 , x.y + 99.4 ),time));
  vec3 c = vec3( s , s1 , s2 );
  return c;

}

//https://github.com/cabbibo/glsl-curl-noise/blob/master/curl.glsl
vec3 curlNoise( vec3 p ){

  const float e = .1;
  vec3 dx = vec3( e   , 0.0 , 0.0 );
  vec3 dy = vec3( 0.0 , e   , 0.0 );
  vec3 dz = vec3( 0.0 , 0.0 , e   );

  vec3 p_x0 = snoiseVec3( p - dx );
  vec3 p_x1 = snoiseVec3( p + dx );
  vec3 p_y0 = snoiseVec3( p - dy );
  vec3 p_y1 = snoiseVec3( p + dy );
  vec3 p_z0 = snoiseVec3( p - dz );
  vec3 p_z1 = snoiseVec3( p + dz );

  float x = p_y1.z - p_y0.z - p_z1.y + p_z0.y;
  float y = p_z1.x - p_z0.x - p_x1.z + p_x0.z;
  float z = p_x1.y - p_x0.y - p_y1.x + p_y0.x;

  const float divisor = 1.0 / ( 2.0 * e );
  return normalize( vec3( x , y , z ) * divisor );

}


uniform float pointSize;
attribute vec3 pos;
attribute vec3 origin;
varying float size;

void main() {
// vec3 p = vec3(0.);
vec3 p = curlNoise(origin);

float noise = snoise(vec4(origin.x,origin.y,origin.z,time));

vec3 newPosition = normalize(p) * (1.0 + 0.10 * noise);
// p.x = curlNoise(origin).x/(distance(origin,vec3(0,0,0))/10.0);
// p.y = curlNoise(origin).y/(distance(origin,vec3(0,0,0))/10.0);
// p.z = curlNoise(origin).z/(distance(origin,vec3(0,0,0))/10.0);
// p = origin + curlNoise(origin);
// p.x = origin.x + snoise(vec2(pos.x/2.0,time))/(distance(origin,vec3(0,0,0))/2.0);
// p.y = origin.y+ snoise(vec2(pos.y/2.0,time))/(distance(origin,vec3(0,0,0))/2.0);
// p.z = origin.z+ snoise(vec2(pos.z/2.0,time))/(distance(origin,vec3(0,0,0))/2.0);
gl_PointSize = size = max( 1., ( step( 1. - ( 1. / 512. ), position.z ) ) * pointSize );
gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );
}
</script>

<script type = 'x-shader-x-fragment' id='fragmentShader'>
uniform vec3 small;
uniform vec3 big;
varying float size;

void main() {
  gl_FragColor = vec4( small, .075 );

  if( size > 1. )
  {
    gl_FragColor = vec4( big * vec3( 1. - length( gl_PointCoord.xy-vec2(.5) ) ) * 1.5, .95 );
  }
}
</script>
<script src='https://threejs.org/build/three.js'></script>
<script src='https://threejs.org/examples/js/controls/OrbitControls.js'></script>
<script src='https://cdnjs.cloudflare.com/ajax/libs/simplex-noise/2.4.0/simplex-noise.min.js'></script>
<script src='https://cdnjs.cloudflare.com/ajax/libs/dat-gui/0.7.3/dat.gui.min.js'></script>
<script src='https://threejs.org/examples/js/postprocessing/EffectComposer.js'></script>
<script src='https://threejs.org/examples/js/shaders/CopyShader.js'></script>
<script src='https://threejs.org/examples/js/postprocessing/RenderPass.js'></script>
<script src='https://threejs.org/examples/js/postprocessing/ShaderPass.js'></script>
<script src='https://threejs.org/examples/js/shaders/FXAAShader.js'></script>
<script src='https://threejs.org/examples/js/shaders/LuminosityHighPassShader.js'></script>
<script src='https://threejs.org/examples/js/postprocessing/UnrealBloomPass.js'></script>
<script src='https://threejs.org/examples/js/loaders/OBJLoader.js'></script>
<script src='https://threejs.org/examples/js/loaders/GLTFLoader.js'></script>



<script  src="{% static "vendor/particular/js/index.js" %}"></script>




</body>

</html>
